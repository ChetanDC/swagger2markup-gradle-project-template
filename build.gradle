import io.github.robwin.swagger2markup.GroupBy

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'http://oss.jfrog.org/artifactory/oss-snapshot-local/' }
        mavenLocal()
    }
    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
        classpath 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.10.1'
        classpath 'io.github.robwin:swagger2markup-gradle-plugin:0.9.2'
        classpath "com.bluepapa32:gradle-watch-plugin:0.1.5"
        classpath "org.kordamp.gradle:livereload-gradle-plugin:0.2.1"
    }
}

apply plugin: 'org.asciidoctor.convert'
apply plugin: 'com.bluepapa32.watch'
apply plugin: 'org.kordamp.gradle.livereload'
apply plugin: 'io.github.robwin.swagger2markup'

group 'io.github.swagger2markup'
version '1.0.0-SNAPSHOT'

repositories {
    jcenter()
    mavenCentral()
}

ext {
    asciiDocOutputDir = file("${buildDir}/asciidoc")
}

convertSwagger2markup {
    pathsGroupedBy GroupBy.TAGS
}

asciidoctor {
    dependsOn convertSwagger2markup
    sources {
        include 'index.adoc'
    }
    backends = ['html5', 'pdf']
    attributes = [
            doctype: 'book',
            toc: 'left',
            toclevels: '3',
            numbered: '',
            sectlinks: '',
            sectanchors: '',
            hardbreaks: '',
            generated: asciiDocOutputDir
    ]
}


watch {
    asciidoc {
        files fileTree('src/docs/asciidoc')
        tasks 'asciidoctor'
    }
}

liveReload {
    docRoot asciidoctor.outputDir.canonicalPath
}


task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}
